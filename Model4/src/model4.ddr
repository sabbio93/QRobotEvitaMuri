RobotSystem m4

Event robotDetected : robotDetected(Sonar,Distanza)
Event robotLeave : robotLeave(Sonar)
Event ostacolo : ostacolo
Event cmd : cmd(X)
Event alarm : alarm(X)
Dispatch comando : comando(X)
Event stop : stop

//Context  ctxController      ip [host="127.0.0.1" port=8040] -httpserver
//Context  ctxRover  			ip [host="localhost" port=8070] 

Context  ctxController      ip [host="192.168.1.2" port=8040] -httpserver
Context  ctxRover  			ip [host="192.168.1.213" port=8070] 
//Context ctxUnity ip [host="localhost" port=8035] -standalone
	
	
//QActor mockstart context ctxController {
//	Plan avvia normal
//		delay time(5000);
//		emit cmd:cmd(start)//;
////		delay time(12000);
////		emit alarm:alarm(fire)
//}
QActor robotmind context ctxController {
    Rules {
		value(incertezza,10).
		
		//salvaDistanzaIniziale(D):-assert(davanti(sonara,rover)),eval(plus,D,10,D1),assign(distanzaIniziale,D1).
		salvaDistanzaIniziale(D):-assert(davanti(sonara,rover)),assign(distanzaIniziale,D).
		
		gap(destra,Delta):-value(distanzaB,D),value(incertezza,I),value(distanzaIniziale,Di),eval(minus,Di,I,Dmin), eval(lt,D,Dmin),!,eval(minus,Dmin,D,Delta).
		gap(sinistra,Delta):-value(distanzaB,D),value(incertezza,I),value(distanzaIniziale,Di),eval(plus,Di,I,DMAX), eval(gt,D,DMAX),!,eval(minus,D,DMAX,Delta).
		gap(no,0).
	}
	Plan init normal
		println("controller starts");
		demo salvaDistanzaIniziale(30);
		switchToPlan aspettaStart
	
	// mi è arrivato un evento dai sonar, cerco di capire cosa è stato rilevato, o reagisco di conseguenza
	Plan aspettaStart
		println("in attesa");
		sense time(600000) robotLeave, robotDetected, cmd -> continue, continue, continue;
		printCurrentEvent;
		
		//modifico la mia conoscenza interna sulla posizione del rover rispetto ai sonar
		[!? davanti(sonara,rover)] onEvent robotLeave:robotLeave(sonara) -> removeRule davanti(sonara,rover)
						   else onEvent robotDetected:robotDetected(sonara,D) -> demo salvaDistanzaIniziale(D);
		[!? davanti(sonara,rover)] println(davanti(sonara,rover));
		[!? value(distanzaIniziale,Di)] println(debug(value(distanzaIniziale, Di)));
		
		//il rover è davanti al sonar a ed ho ricevuto il comando start
		[!? davanti(sonara,rover)] onEvent cmd:cmd(start) -> forward rover -m comando:comando(start);
		[!? davanti(sonara,rover)] onEvent cmd:cmd(start) -> switchToPlan aspettaEventi;
		repeatPlan
	
	// mi è arrivato un evento dai sonar, cerco di capire cosa è stato rilevato, o reagisco di conseguenza
	Plan aspettaEventi
		println("in ascolto");
		sense time(600000) robotLeave, robotDetected, cmd, ostacolo -> continue, continue, continue, continue;
		printCurrentEvent;
		
		//il rover è davanti al sonar a ed ho ricevuto il comando start
		[!? davanti(sonara,rover)] println(davanti(sonara,rover));
		
		//modifico la mia conoscenza interna sulla posizione del rover rispetto ai sonar
		[!? davanti(sonara,rover)] onEvent robotLeave:robotLeave(sonara) -> removeRule davanti(sonara,rover);
		[!? value(distanzaIniziale,Di)] println(debug(value(distanzaIniziale, Di)));
		
		onEvent ostacolo:ostacolo -> switchToPlan valutaOstacolo;
		onEvent robotDetected:robotDetected(sonarb,D) -> demo assign(distanzaB,D); //arrivato quasi a destinazione
		onEvent robotDetected:robotDetected(sonarb,D) -> emit stop:stop;
		onEvent robotDetected:robotDetected(sonarb,D) -> switchToPlan risolviGap;
		repeatPlan
	
	Plan valutaOstacolo resumeLastPlan
		delay time(2000);
		sense time(500) ostacolo -> continue;
		[!? tout(senseevent,QA)] forward rover -m comando:comando(start);
		[?? tout(senseevent,QA)] endPlan "";
		onEvent ostacolo : ostacolo -> println("Ostacolo fisso");
		onEvent ostacolo : ostacolo -> endQActor "Incontrato un ostacolo fisso"	//In realt� nell'attuale implementazione di qactor non termina
		
	Plan risolviGap
		println("piano risolviGap");
		demo gap(Dir,Dist);
		[!? goalResult(G)] println(G);
		[?? goalResult(gap(destra,D))] forward rover -m comando:comando(destra);
		[?? goalResult(gap(sinistra,D))] forward rover -m comando:comando(sinistra);
		[?? goalResult(gap(no,D))] endPlan "" else switchToPlan controlloPosizione
	
	Plan controlloPosizione
		println("piano controlloPosizione");
		sense time(600000) robotDetected, ostacolo -> continue, continue;
		printCurrentEvent;
		onEvent ostacolo:ostacolo -> switchToPlan valutaOstacolo;
		onEvent robotDetected:robotDetected(sonarb,D) -> demo assign(distanzaB,D);
		onEvent robotDetected:robotDetected(sonarb,D) -> demo gap(Dir,Dist);
		[!? goalResult(gap(Dir,D))] println(D);
		[?? goalResult(gap(Dir,0))] onEvent robotDetected:robotDetected(sonarb,D) -> emit stop:stop else repeatPlan
}

Robot swag QActor rover context ctxRover {	 
	Rules {
		//unity(Host) :- getCtxHost(ctxUnity, Host).
	}
	Plan init normal
		[!?unity(IP)] switchToPlan initUnity else println("Rover pronto");
		switchToPlan aspettaComando
		
	Plan initUnity resumeLastPlan
		[!?unity(IP)] actorOp workWithUnity(IP);
		actorOp initPosition;
		robotRight speed(100) time(2000);
		println("Inizializzazione Unity completata")
	/*
	 * R2: Il robot comincia a muoversi alla pressione del tasto start
	 * R3: solo se è davanti al sonarA
	 */
	Plan aspettaComando //normal
		println("attesa comandi");     
		receiveMsg time(600000);
		onMsg comando:comando(start) -> switchToPlan traversata;
		onMsg comando:comando(destra) -> switchToPlan recuperaDx;
		onMsg comando:comando(sinistra) -> switchToPlan recuperaSx;
		repeatPlan
		
	/*
	 * R1: Il robot si muove in avanti fino a quando non è arrivato a destinazione
	 */		
	Plan traversata
		println("Inizio traversata");
		//15 unity, 100 swag
		robotForward speed(100) time(600000) react event stop -> fermaRobot or event alarm -> gestisciAllarme or event ostacolo -> fermaRobot
	
	Plan fermaRobot
		println("Stop requested");
		robotStop speed(0) time(0);
		switchToPlan aspettaComando
		
	Plan recuperaDx
		//1000 unity, 500 swag
		robotRight speed(100) time(500) react event stop -> fermaRobot or event alarm -> gestisciAllarme or event ostacolo -> fermaRobot;
		switchToPlan traversata
	
	Plan recuperaSx
		//1000 unity, 500 swag
		robotLeft speed(100) time(500) react event stop -> fermaRobot or event alarm -> gestisciAllarme or event ostacolo -> fermaRobot;
		switchToPlan traversata
    
    Plan gestisciAllarme
    	println("ALLARME! ALLARME!");
		robotStop speed(0) time(0)
}
